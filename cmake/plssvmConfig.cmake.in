## Authors: Alexander Van Craen, Marcel Breyer
## Copyright (C): 2018-today The PLSSVM project - All Rights Reserved
## License: This file is part of the PLSSVM project which is released under the MIT license.
##          See the LICENSE.md file in the project root for full license information.
########################################################################################################################

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(OpenMP QUIET)

# check if the OpenMP backend is required
set(PLSSVM_HAS_OPENMP_BACKEND @PLSSVM_OPENMP_BACKEND_LIBRARY_NAME@)
if(PLSSVM_HAS_OPENMP_BACKEND)
    find_dependency(OpenMP REQUIRED)
endif()

# check if fmt has been installed via FetchContent
set(PLSSVM_FOUND_FMT @fmt_FOUND@)
if(PLSSVM_FOUND_FMT)
    find_dependency(fmt REQUIRED)
endif()

# check if the CUDA backend is required
set(PLSSVM_HAS_CUDA_BACKEND @PLSSVM_CUDA_BACKEND_LIBRARY_NAME@)
if(PLSSVM_HAS_CUDA_BACKEND)
    enable_language(CUDA)
endif()

# check if the OpenCL backend is required
set(PLSSVM_HAS_OPENCL_BACKEND @PLSSVM_OPENCL_BACKEND_LIBRARY_NAME@)
if(PLSSVM_HAS_OPENCL_BACKEND)
    find_dependency(OpenCL REQUIRED)
endif()

# check if the SYCL implementation hipSYCL backend is required
set(PLSSVM_HAS_SYCL_BACKEND_HIPSYCL @PLSSVM_SYCL_BACKEND_HIPSYCL_LIBRARY_NAME@)
if(PLSSVM_HAS_SYCL_BACKEND_HIPSYCL)
    set(HIPSYCL_TARGETS @HIPSYCL_TARGETS@)
    find_dependency(hipSYCL CONFIG REQUIRED)
    message(STATUS "Found hipSYCL with ${HIPSYCL_TARGETS}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/plssvmTargets.cmake")
check_required_components("plssvm")